// Jenkinsfile for MLBParks
podTemplate(
  label: "skopeo-pod",
  cloud: "openshift",
  inheritFrom: "maven",
  containers: [
    containerTemplate(
      name: "jnlp",
      image: "docker-registry.default.svc:5000/${GUID}-jenkins/jenkins-slave-pod",
      resourceRequestMemory: "1Gi",
      resourceLimitMemory: "2Gi"
    )
  ]
) {
  node('skopeo-pod') {
    echo "GUID: ${GUID}"
    echo "CLUSTER: ${CLUSTER}"

    // Your Pipeline Code goes here. Make sure to use the ${GUID} and ${CLUSTER} parameters where appropriate
    // You need to build the application in directory `MLBParks`.
    // Also copy "../nexus_settings.xml" to your build directory
    // and replace 'GUID' in the file with your ${GUID} to point to >your< Nexus instance
 
    def mvnCmd = "mvn -s ./nexus_settings.xml"

    stage('Checkout Source Code') {
      echo "Checking out source code..."
      git url: 'https://github.com/xingguang2013/advdev_homework_template.git'
    }

    def groupId = getGroupIdFromPom("./MLBParks/pom.xml")
    def artifactId = getArtifactIdFromPom("./MLBParks/pom.xml")
    def version = getVersionFromPom("./MLBParks/pom.xml")

    def devTag = "${version}-${BUILD_NUMBER}"
    def prodTag = "${version}"

    stage('Build Java War') {
      echo "Building java war version ${devTag}..."
      sh "${mvnCmd} -f ./MLBParks/pom.xml -DskipTests clean package"
    }

    stage('Run Unit Tests') {
      echo "Running unit tests..."
      sh "${mvnCmd} -f ./MLBParks/pom.xml test"
    }

    stage('Sonarqube Code Analysis') {
      echo "Analyzing code use sonarqube..."
      sh "${mvnCmd} -f ./MLBParks/pom.xml sonar:sonar -Dsonar.host.url=http://sonarqube-${GUID}-sonarqube.apps.${CLUSTER} -Dsonar.projectName=${JOB_BASE_NAME}-${devTag}"
    }

    stage('Publish WAR Artifact To Nexus') {
      echo "Publishing artifact to nexus..."
      sh "${mvnCmd} -f ./MLBParks/pom.xml deploy -DskipTests=true -DaltDeploymentRepository=nexus::default::http://nexus3-${GUID}-nexus.apps.${CLUSTER}/repository/releases"
    }

    stage('Build and Tag OpenShift Image') {
      echo "Building OpenShift container image tasks:${devTag}..."
      // sh "oc start-build mlbparks --follow --from-file=./MLBParks/target/mlbparks.war -n ${GUID}-parks-dev"
      sh "oc start-build mlbparks --follow --from-file=http://nexus3-${GUID}-nexus.apps.${CLUSTER}/repository/releases/com/openshift/evg/roadshow/mlbparks/${version}/mlbparks-${version}.war -n ${GUID}-parks-dev"
      openshiftTag alias: 'false', destStream: 'mlbparks', destTag: devTag, destinationNamespace: "${GUID}-parks-dev", namespace: "${GUID}-parks-dev", srcStream: 'mlbparks', srcTag: 'latest', verbose: 'false'
    }

    stage('Verify Deploy In Development') {
      echo "Deploying container image to Development Project..."
      sh "oc set image dc/mlbparks mlbparks=docker-registry.default.svc:5000/${GUID}-parks-dev/mlbparks:${devTag} -n ${GUID}-parks-dev"
      
      sh "oc delete configmap mlbparks-config -n ${GUID}-parks-dev --ignore-not-found=true"
      sh "oc create configmap mlbparks-config \
                    --from-literal=APPNAME='MLB Parks (Dev)' \
                    --from-literal=DB_HOST=mongodb.${GUID}-parks-dev.svc \
                    --from-literal=DB_NAME=mongodb \
                    --from-literal=DB_PASSWORD=mongodb \
                    --from-literal=DB_PORT=27017 \
                    --from-literal=DB_USERNAME=mongodb \
                    -n ${GUID}-parks-dev"
      sh "oc set env dc/mlbparks --from=configmap/mlbparks-config -n ${GUID}-parks-dev" 
      
      openshiftDeploy depCfg: 'mlbparks', namespace: "${GUID}-parks-dev", verbose: 'false', waitTime: '20', waitUnit: 'min'
      openshiftVerifyDeployment depCfg: 'mlbparks', namespace: "${GUID}-parks-dev", replicaCount: '1', verbose: 'false', verifyReplicaCount: 'false', waitTime: '20', waitUnit: 'min'
      openshiftVerifyService namespace: "${GUID}-parks-dev", svcName: 'mlbparks', verbose: 'false'
    }
    
    stage('Run Integration Tests') {
      echo "Sleep 60s waiting for the app has started..."
      sleep 60

      echo "Running Integration Tests..."
      sh "curl http://mlbparks-${GUID}-parks-dev.apps.${CLUSTER}/ws/healthz/ | grep -q 'OK'"
      sh "curl http://mlbparks-${GUID}-parks-dev.apps.${CLUSTER}/ws/data/load/ | grep -q 'Items inserted in database: 30'"
    }

    stage('Copy Image to Nexus Docker Registry') {
      echo "Copying image to Nexus Docker Registry..."
      sh "skopeo copy --src-tls-verify=false --dest-tls-verify=false --src-creds openshift:\$(oc whoami -t) --dest-creds admin:admin123 docker://docker-registry.default.svc.cluster.local:5000/${GUID}-parks-dev/mlbparks:${devTag} docker://nexus-registry-${GUID}-nexus.apps.${CLUSTER}/mlbparks:${devTag}"
      openshiftTag alias: 'false', destStream: 'mlbparks', destTag: prodTag, destinationNamespace: "${GUID}-parks-dev", namespace: "${GUID}-parks-dev", srcStream: 'mlbparks', srcTag: devTag, verbose: 'false'
    }

    def destApp   = "mlbparks-green"
    def destcfgMap = "Green"
    def activeApp = ""

    stage('Blue/Green Production Deployment') {
      echo "Deploying Blue/Green Production..."
      activeApp = sh(returnStdout: true, script: "oc get route mlbparks -n ${GUID}-parks-prod -o jsonpath='{ .spec.to.name }'").trim()
      
      if (activeApp == "mlbparks-green") {
        destApp = "mlbparks-blue"
        destcfgMap = "Blue"
      }
      
      echo "Active Application:      " + activeApp
      echo "Destination Application: " + destApp
      echo "Config Map to be used:   " + destcfgMap
      
      sh "oc set image dc/${destApp} ${destApp}=docker-registry.default.svc:5000/${GUID}-parks-dev/mlbparks:${prodTag} -n ${GUID}-parks-prod"
      
      sh "oc delete configmap mlbparks-config -n ${GUID}-parks-prod --ignore-not-found=true"
      sh "oc create configmap mlbparks-config \
                    --from-literal=APPNAME='MLB Parks (${destcfgMap})' \
                    --from-literal=DB_HOST=mongodb \
                    --from-literal=DB_NAME=mongodb \
                    --from-literal=DB_PASSWORD=mongodb \
                    --from-literal=DB_PORT=27017 \
                    --from-literal=DB_USERNAME=mongodb \
                    --from-literal=DB_REPLICASET=rs0 \
                    -n ${GUID}-parks-prod"
      sh "oc set env dc/${destApp} --from=configmap/mlbparks-config -n ${GUID}-parks-prod"               

      sh "oc expose dc ${destApp} --port 8080 --labels=type=parksmap-backend -n ${GUID}-parks-prod" 

      openshiftDeploy depCfg: destApp, namespace: "${GUID}-parks-prod", verbose: 'false', waitTime: '20', waitUnit: 'min'
      openshiftVerifyDeployment depCfg: destApp, namespace: "${GUID}-parks-prod", replicaCount: '1', verbose: 'false', verifyReplicaCount: 'true', waitTime: '20', waitUnit: 'min'
      openshiftVerifyService namespace: "${GUID}-parks-prod", svcName: destApp, verbose: 'false'
    }

    stage('Switch route to new Version') {
      echo "Switching Production application to ${destApp}..."
      sh "oc patch route mlbparks -n ${GUID}-parks-prod -p '{\"spec\":{\"to\":{\"name\":\"" + destApp + "\"}}}'"
      sh "oc delete svc ${activeApp} -n ${GUID}-parks-prod"
    }
  }
}

// Convenience Functions to read variables from the pom.xml
// Do not change anything below this line.
def getVersionFromPom(pom) {
  def matcher = readFile(pom) =~ '<version>(.+)</version>'
  matcher ? matcher[0][1] : null
}
def getGroupIdFromPom(pom) {
  def matcher = readFile(pom) =~ '<groupId>(.+)</groupId>'
  matcher ? matcher[0][1] : null
}
def getArtifactIdFromPom(pom) {
  def matcher = readFile(pom) =~ '<artifactId>(.+)</artifactId>'
  matcher ? matcher[0][1] : null
}
